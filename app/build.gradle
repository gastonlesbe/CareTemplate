plugins {
    id("com.android.application")
    id("com.google.gms.google-services")
}

android {
    namespace 'com.gastonlesbegueris.caretemplate'

    // Que compile y apunte a la misma API (evita warnings raros)
    compileSdk 35

    defaultConfig {
        applicationId "com.gastonlesbegueris.caretemplate"
        minSdk 24
        targetSdk 35   //  igual o menor a compileSdk
        versionCode 11
        versionName "2.0"
        vectorDrawables.useSupportLibrary = true

    }

    // Java 17 para evitar el warning de "source value 8 is obsolete"
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // Flavors: uno solo, sin bloques anidados
    flavorDimensions "vertical"
    productFlavors {
        pets {
            dimension "vertical"
            applicationIdSuffix ".pets"
            resValue "string", "app_name", "CarePal: Pets"
            resValue "string", "app_type", "pets"     //  para reemplazar BuildConfig.FLAVOR
            resValue "string", "admob_app_id", "ca-app-pub-9841764898906750~2089158049"
            resValue "string", "admob_interstitial_id", "ca-app-pub-9841764898906750/6449521527"
        }
        cars {
            dimension "vertical"
            applicationIdSuffix ".cars"
            resValue "string", "app_name", "CarePal: Cars"
            resValue "string", "app_type", "cars"
            resValue "string", "admob_app_id", "ca-app-pub-9841764898906750~2089158049"
            resValue "string", "admob_interstitial_id", "ca-app-pub-9841764898906750/7520553240"
        }
        family {
            dimension "vertical"
            applicationIdSuffix ".family"
            resValue "string", "app_name", "CarePal Family"
            resValue "string", "app_type", "family"
            resValue "string", "admob_app_id", "ca-app-pub-9841764898906750~9388593714"
            resValue "string", "admob_interstitial_id", "ca-app-pub-9841764898906750/6799440463"
        }
        house {
            dimension "vertical"
            applicationIdSuffix ".house"
            resValue "string", "app_name", "CarePal: Houses"
            resValue "string", "app_type", "house"
            resValue "string", "admob_app_id", "ca-app-pub-9841764898906750~2089158049"
            resValue "string", "admob_interstitial_id", "ca-app-pub-9841764898906750/9832458605"
        }
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    // Configuraci贸n de firma para release
    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            def keystoreProperties = new Properties()
            if (keystorePropertiesFile.exists()) {
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // Generar s铆mbolos nativos para depuraci贸n
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    
    // Disable baseline profile to avoid INSTALL_BASELINE_PROFILE_FAILED errors
    // This prevents issues when installing on some devices
    packaging {
        jniLibs {
            useLegacyPackaging = true
        }
        // Exclude baseline profile files that can cause installation issues
        resources {
            excludes += ['**/baseline.prof', '**/*.prof']
        }
    }

    // No hace falta fijar buildToolsVersion manualmente; lo elige AGP
    // buildToolsVersion '36.1.0'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'


    // Material: usa UNA sola versi贸n
    implementation 'com.google.android.material:material:1.13.0'

    // Firebase (BoM + m贸dulos)
    implementation platform('com.google.firebase:firebase-bom:34.7.0')
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-analytics'

    // Room (Java)
    implementation 'androidx.room:room-runtime:2.8.4'
    annotationProcessor 'androidx.room:room-compiler:2.8.4'

    // Ads - Appodeal SDK (replaces AdMob)
    implementation 'com.appodeal.ads:sdk:3.12.0.0'
    
    // AdMob SDK for interstitial ads
    implementation 'com.google.android.gms:play-services-ads:24.9.0'
   // implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // Google Play In-App Review
    implementation 'com.google.android.play:review:2.0.2'

}
